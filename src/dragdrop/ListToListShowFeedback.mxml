<?xml version="1.0" encoding="utf-8"?>
<!-- dragdrop\ListToListShowFeedback.mxml -->
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" 
				creationComplete="initApp();">
	
	<mx:Script>
		<![CDATA[
			import mx.managers.DragManager;
			import mx.events.DragEvent;
			import mx.collections.ArrayCollection;
			
			private function initApp():void {
				firstList.dataProvider = new ArrayCollection([
					{label:"第一列", data:"1"},
					{label:"第二列", data:"2"},
					{label:"第三列", data:"3"},
					{label:"第四列", data:"4"}
				]);
				secondList.dataProvider = new ArrayCollection([]);
			}
			
			private var tempBorderColor:uint;
			private var borderColorSet:Boolean = false;
			
			private function dragOverHandler(event:DragEvent):void {
				event.preventDefault();
				
				// 调用showDropFeedback(event)来显示放置标记。
				event.currentTarget.showDropFeedback(event);
				
				if (event.dragSource.hasFormat("items"))
				{
					if (borderColorSet == false) {                 
						tempBorderColor = 
							event.currentTarget.getStyle('borderColor');
						borderColorSet = true;
					}
					
					// 设定拖动回复的标记，并改变放置目标外框为红色。
					event.currentTarget.setStyle('borderColor', 'red');
					if (event.ctrlKey) {                    
						DragManager.showFeedback(DragManager.COPY);
						return;
					}
					else if (event.shiftKey) {
						DragManager.showFeedback(DragManager.LINK);
						return;
					}
					else {
						DragManager.showFeedback(DragManager.MOVE);
						return;
					}
				}
				
				// 标记不能放置。
				DragManager.showFeedback(DragManager.NONE);                
			}
			
			private function dragDropHandler(event:DragEvent):void {
				dragExitHandler(event);
			}            
			
			// 恢复外框的颜色。
			private function dragExitHandler(event:DragEvent):void {
				event.currentTarget.setStyle('borderColor', tempBorderColor);
				borderColorSet = true;
			}
		]]>
	</mx:Script>
	<mx:Panel 
		title="改变放置目标属性示例" width="90%" height="90%"
		horizontalAlign="center"
		verticalAlign="middle">   
		<mx:HBox id="myHB">
			<mx:List  id="firstList" 
					  dragEnabled="true"/>
			
			<mx:List  id="secondList"
					  dropEnabled="true"
					  dragMoveEnabled="true"
					  dragOver="dragOverHandler(event);"
					  dragDrop="dragExitHandler(event);"
					  dragExit="dragExitHandler(event);"/>
		</mx:HBox>
	</mx:Panel>
</mx:Application>